[IntrabarOrderGeneration = true]
inputs:
	takeR3				(true),
	takeS3				(true),
	takeLzbrk			(true),
	takeSz 				(true),
	takeLz  			(true),
	fadeOhigh			(true),
	StartGlobex			(1500),
	EndGlobex			(0630),
	StartOpeningSession	(0630),
	EndOpeningSession	(0730),
	emalen				(75); // 750 minutes worth

variables:
    double ds1( 0 ),
	double ds2( 0 ),
	double ds3( 0 ),
	double dr1( 0 ),
	double dr2( 0 ),
	double dr3( 0 ),
	double dp( 0 ),
	double TodaysHigh( 0 ),
	double YestHigh( 0 ),
	double TodaysLow( 0 ),
	double YestLow( 0 ),
	double TodaysClose( 0 ),
	double YestClose( 0 ),
	int Counter( 0 ),
	noTrade (false);


vars: shigh(0), slow(9999), o_high(0), o_low(9999), CurrSess(0), CalcTrigger(false),
 oh(0), ol(0), sh(0), sl(0);

variables:
	bool r3_shoholsl  (false),
	bool s3_shoholsl  (false),
	bool r3_shohslol  (false),
	bool s3_shohslol  (false),
	bool r3_ohsholsl (false),
	bool s3_ohsholsl (false),
	bool r3_ohshslol  (false),
	bool s3_ohshslol  (false),
	bool useR2_lowstop 	(false),
	bool useR3_lowstop 	(false),
	bool usePP_lowstop	(false),
	bool useSH_lowstop	(false),
	bool useS1_lowstop	(false),
	bool useS1_highstop (false),
	bool useS2_highstop (false),
	bool useR3_highstop	(false),
	bool usePP_highstop	(false),
	bool useS2_lowstop	(false),
	bool lzbrk_shoholsl (false),
	bool lz_shohslol 	(false),
	bool fade_ohsholsl	(false),
	bool useR2_highstop (false),
	bool useEma50_highstop (false),
	bool usePP_wide_lowstop (false),
	bool useOL_wide_lowstop	(false),
	bool o_low_wide_stop_long (false),
	double wide_highstop_v (0),
	double generic_lowstop_v (0),
	bool useGeneric_highstop	(false),
	bool useGeneric_lowstop (false),
	bool useOH_wide_highstop (false),
	double generic_highstop_v (0),
	double generic_ema50_highstop_v (0),
	bool useEma50_lowstop (false),
	double generic_ema50_lowstop_v (0),
	double wide_lowstop_v (-1),
	bool dp_limit (false),
	bool stop_brkt_ohsholsl (false),
	bool wide_highstop (false),
	bool r3_brk_ohsholsl (false),
	bool wide_lowstop_brk (false),
	bool ema20_x_wlowstop (false),
	bool ema20_x_openlow (false),
	bool fadex_ohsholsl	(false);

CurrSess = CurrentSession( 0 );
CalcTrigger = CurrSess <> CurrSess[1];
if CalcTrigger then
Begin
	o_high = 0;
	o_low = 999999;
	shigh = 0;
	slow = 999999;
	wide_lowstop_v = -1;
	wide_highstop_v = -1;
	r3_shoholsl = false;
	s3_shoholsl = false;
	r3_shohslol = false;
	s3_shohslol = false;
	r3_ohsholsl = false;
	s3_ohsholsl = false;
	r3_ohshslol = false;
	s3_ohshslol = false;
	ema20_x_openlow = false;
	wide_lowstop_brk = false;
	r3_brk_ohsholsl = false;
	wide_highstop = false;
	o_low_wide_stop_long = false;
	useR2_highstop = true;
	lzbrk_shoholsl = false;
	lz_shohslol = false;
	fade_ohsholsl = false;
	fadex_ohsholsl = false;
	ema20_x_wlowstop = false;
	noTrade = false;
	fade_ohsholsl = false;
	fadex_ohsholsl = false;
	dp_limit = false;
	stop_brkt_ohsholsl = false;
End;
vars: open_d(0);


{globex swing block}
If Time >= StartGlobex or Time < StartOpeningSession then begin
	If Low < slow then slow = Low;
	If High >= shigh then shigh = High;
end;
{opening swing block}
If Time >= StartOpeningSession and Time <= EndOpeningSession then begin
	If Low < o_low then o_low = Low;
	If High >= o_high then o_high = High;
end;

vars: basis(0), dev(0), u236(0), u382(0), u5(0), u618(0), u764(0),
HighSinceOpen(0), LowSinceOpen(9999);
// fib pivots
if CalcTrigger then
begin
	if CurrentBar > 1 then
		Counter = Counter + 1;

	YestHigh = TodaysHigh;
	YestLow = TodaysLow;
	YestClose = Close;
	TodaysHigh = High;
   	TodaysLow = Low;

	LowSinceOpen = 9999;
	HighSinceOpen = 0;

	dp = ( YestHigh + YestLow + YestClose ) / 3;
	dr1 = dp + 0.382 * (YestHigh - YestLow);
	dr2 = dp + 0.618 * (YestHigh - YestLow);
	dr3 = dp + (YestHigh - YestLow) ;
	ds1 = dp - 0.382 * (YestHigh - YestLow);
	ds2 = dp - 0.618 * (YestHigh - YestLow);
	ds3 = dp - (YestHigh - YestLow) ;


end
else
begin
	if High > TodaysHigh then
		TodaysHigh = High;
	if Low < TodaysLow then
		TodaysLow = Low;
end;
if time >= StartOpeningSession and time < StartGlobex then Begin
	if High > HighSinceOpen then
		HighSinceOpen = High;
	if Low < LowSinceOpen then
		LowSinceOpen = Low;
end;
if time >= StartOpeningSession then Begin
	basis = slow;
	dev = shigh-slow;
	u236 = basis + (0.236*dev); // between slow and u236 is buy zone
	u382 = basis + (0.382*dev);
	u5 = basis + (0.5*dev);
	u618 = basis + (0.618*dev);
	u764 = basis + (0.764*dev); // between shigh adn u764 is sell zone
end;
if month(date) = 11 and Dayofmonth(date) = 28 Then
	noTrade = true;
vars: o_range(0), s_range(0);
// measure the opening swing
o_range = o_high - o_low;
s_range = shigh - slow;
var: ema20(0), emaX(0), exit_v(0);
emaX = XAverage(close, emalen);
ema20 = XAverage(close, 20);

if time >= StartOpeningSession and time < EndOpeningSession then Begin

    if Open > shigh and open > ema20 and ema20 > emaX Then
    	buy ("open_higher_shigh") this bar on close;
    {if Open < slow Then
    	sell short ("open_lower_slow") this bar on close;	}

end;

if noTrade = false and time >= EndOpeningSession and time <= 1230 then begin

	{TBDs}
	// check lower wide stops aka lower lows in the Overnight session
	// correlate this trend with the EMA 50 and observe
	// opening action wrt to EMA 50 breach or hold.
	// EMA50-shorts
	// dates: 08/10/2018
	// EMA50-longs
	// dates: 08/17/2018, 08/21/2018, etc

	if useOH_wide_highstop = true and o_low_wide_stop_long = false then Begin
		if close crosses above wide_highstop_v + 2 then begin
			//buy ("o_high_wide_stop") this bar on close;
			//o_low_wide_stop_long = true;
		end;
	end;
	{**** OPENING LOW DOES NOT TEST NEARBY PIVOT DURING OPENING SWING ***}
	if o_low > dp and wide_lowstop_v > dp and LowSinceOpen > dp
	Then
	Begin
		// buy the first test of the daily pivot
		//buy ("dp_limit") next bar at dp+0.25 limit;
		//dp_limit = true;
	end;

	{*** INSIDE case revert when shigh is higher than the opening high *** }
    if shigh > o_high and slow < o_low  then begin
    	// need to see a lower high stop
    	if generic_lowstop_v > generic_lowstop_v[1]
    		and close crosses above wide_highstop_v and wide_highstop = false Then
    	begin
    		buy ("wide_highstop_brk") this bar on close;
    		wide_highstop = true;
    	end;
    	if useR2_lowstop = false and takeR3 and lzbrk_shoholsl = false
    		and close crosses below dr3  and dr3 >= o_high then begin //and not r3_done
           	sell short ("r3_shoholsl") at this bar;
           	r3_shoholsl = true;
        end;
        if useS2_highstop = false and takeS3
        	and close > close[1] and close crosses above ds3 + 1
        	and ds3 <= o_low then begin // or limit
        	buy ("s3_shoholsl") at this bar ;
        	s3_shoholsl = true;
        end;
        // buy breakout only if high does not hold and o_high is not a threat either
        if takeLzbrk = true and lzbrk_shoholsl = false
        	and low crosses above o_high and close > o_high
        	and o_high > dr2 then Begin
        	buy ("lzbrk_shoholsl") at this bar;
        	lzbrk_shoholsl = true;
        end;
        // if high in sell zone and by 11:00 there is no breakout
        if takeSz and (u764 > dr1 and high > u764)
        	and high crosses under o_high
        	and (lzbrk_shoholsl = false and time >= 1100) then Begin
        	sell short ("sz_shoholsl") at this bar;
        end;

		if useOL_wide_lowstop = true and fade_ohsholsl = false and o_low_wide_stop_long = false
			and dp > o_low and ds1 >= o_low then Begin
			if low crosses above wide_lowstop_v then begin
				buy ("olow_wstop_shoholsl") this bar on close;
				o_low_wide_stop_long = true;
			end;
		end;
    end;
	 // BREAKOUT: it might already be over by 730, bruh!
	 // case breakout upside or fakeout when opening high trounces overnight high (shigh)
    if shigh < o_high and slow < o_low then begin
    	if close crosses under wide_lowstop_v
    		and wide_lowstop_v > o_high Then
    		sell short ("ohbrk_ohsholsl") this bar on close;

   		if fadeOhigh = true Then
   		Begin
   			// did the opening swing close at o_high?
   			if high = o_high and o_high >= dr3 - 3 //and close crosses under o_high
   				Then begin
   				sell short ("fadex_ohsholsl") next bar on dr3 - 3 stop;
   				fade_ohsholsl = true;
   			end;
   		end;
   		if useR2_lowstop = false and takeR3
   			and close crosses below dr3 and dr3 >= o_high
   			and HighSinceOpen <= dr3 + 3 // not a major break to dr3
   			then begin
           		sell short ("r3_ohsholsl") at this bar;//next bar o_high limit;
           		r3_ohsholsl = true;
        {end else Begin
        	// in this case reverse course and buy at dr3
        	if close crosses below dr3 and dr3 >= o_high and r3_brk_ohsholsl = false
        	  and HighSinceOpen > dr3 + 3 then
        		buy ("r3_brk_ohsholsl") at this bar;
        		r3_brk_ohsholsl = true;}
        end;

        if useS2_highstop = false
        	and takeS3 and low crosses above ds3 +1 and ds3 <= o_low then begin // or limit
        	buy ("s3_ohsholsl") at this bar ;
        	s3_ohsholsl = true;
        end;
    end;
	// when opening low lower than overnight low and opening high lower than overnight high
	// case: breakout to downside or fakeout
    if shigh > o_high and slow > o_low then begin

        // first check if r3 virgin test short works
        if useR2_lowstop = false and takeR3
        	and close crosses below dr3 and dr3 >= o_high
   			and HighSinceOpen >= dr3 + 1 // no squeeze expected
   			then begin
           		sell short ("r3_shohslol") at this bar;
           		r3_shohslol = true;
        end;

        // too agressive
        {if close crosses under wide_lowstop_v and wide_lowstop_brk = false Then
        begin
        	sell short ("wide_lowstop_brk") this bar on close;
        	wide_lowstop_brk = true;
        end;}

    end;
     // case where both directions were squeezed!
    if shigh < o_high and slow > o_low then begin
    	// cross below dr3 only works if dr3 not substantially broken
    	// how high did the ES break dr3?
    	// where is the EMA and how was the overnight trend?
    	if useR2_lowstop = false and takeR3
    		and close crosses below dr3 and dr3 >= o_high
   			and HighSinceOpen < dr3 + 3 // not major break of dr3
    	then begin //and not r3_done
        	sell short ("r3_ohshslol") at this bar;
           	r3_ohshslol = true;
        end else Begin
        	// in this case reverse course and buy at dr3
        	if close crosses below dr3 and dr3 >= o_high
        	  and HighSinceOpen >= dr3 + 3 then
        		buy ("r3_brk_ohshslol") at this bar;

        end;

       if useS2_highstop = false and takeS3 and low crosses above ds3 + 1  and ds3 <= o_low then begin // or limit
        	buy ("s3_ohshslol") at this bar ;
        	s3_ohshslol = true;
        end;

	end;

	if useOL_wide_lowstop = true and fade_ohsholsl = false and o_low_wide_stop_long = false
		and dp > o_low and ds1 >= o_low then Begin
		if low crosses above wide_lowstop_v then begin
			buy ("olow_wstop") this bar on close;
			o_low_wide_stop_long = true;
		end;
		if low crosses above slow + 1
			and (
				(slow <= ds2 and low <= ds1) or
				(slow <= ds3 and low <= ds2)
				)
			then begin
			buy ("olow_slow") this bar on close;
			o_low_wide_stop_long = true;
		end;
	end;
	// the stop needs to be tight based on testing this setup
	if wide_lowstop_v <> -1 and emaX > ema20 and ema20 crosses under wide_lowstop_v Then
   	begin
   	 	sell short ("ema20_x_wlowstop") at this bar;
   	 	ema20_x_wlowstop = true;
   	 end;
   	if wide_lowstop_v = -1 and emaX > ema20 and ema20 crosses under o_low Then
   	begin
   	 	sell short ("ema20_x_openlow") at this bar;
   	 	ema20_x_openlow = true;
   	end;
end;

if time >= StartOpeningSession and time <= 1300 then begin
	usePP_lowstop = false;
	usePP_highstop = false;
	useR2_lowstop = false;
	useR3_lowstop = false;
	useSH_lowstop = false;
	useS1_lowstop = false;
	useS1_highstop = false;
	useS2_highstop = false;
	useR2_highstop = false;
	useR3_highstop = false;
	useS2_lowstop = false;
	useGeneric_highstop = false;
	useGeneric_lowstop = false;
	generic_highstop_v = 0;
	generic_ema50_highstop_v = 0;
	generic_lowstop_v = 0;
	usePP_wide_lowstop = false;
	useOL_wide_lowstop = false;
	useOH_wide_highstop = false;
	useEma50_highstop = false;
	useEma50_lowstop = false;

	{************S T O P S***************
	*************************************}

	// check if any new lows became a stop
	var: stop_v (0), openBarsAgo(-1), wide_lowstop_v_barsago(-1), wide_highstop_v_barsago(-1);

	openBarsAgo = FindBar(Date, StartOpeningSession);


	// swinglow stops

	// only compute the wide stop once per trading session
	if wide_lowstop_v = -1 then begin
		wide_lowstop_v = SwingLow(1, low, 5, 50);
		// this stop must have been created by WallStreet traders
		wide_lowstop_v_barsago = SwingLowBar(1, low, 5, 50);
		if openBarsAgo = -1 or wide_lowstop_v_barsago = -1
			or openBarsAgo < wide_lowstop_v_barsago Then
			wide_lowstop_v = -1;
	end;

	if wide_lowstop_v <> -1 Then
		generic_lowstop_v = wide_lowstop_v;
	if wide_lowstop_v <> -1 and wide_lowstop_v >= dp - 2 and wide_lowstop_v <= dp + 2 Then
		usePP_wide_lowstop = true;
	if wide_lowstop_v <> -1 and wide_lowstop_v >= o_low - 1 and wide_lowstop_v <= o_low + 1 Then
		useOL_wide_lowstop = true;


	stop_v = SwingLow(1, low, 2, 20);
	if stop_v <> -1 and stop_v >= dr2 - 2 and stop_v <= dr2 + 2 Then
		useR2_lowstop = true;
	if stop_v <> -1 and stop_v >= dr3 - 2 and stop_v <= dr3 + 2 Then
		useR3_lowstop = true;
	if stop_v <> -1 and stop_v >= dp - 1 and stop_v <= dp + 1 Then
		usePP_lowstop = true;
	if stop_v <> -1 and stop_v >= ds1 - 2 and stop_v <= ds1 + 2 Then
		useS1_lowstop = true;
	if stop_v <> -1 and stop_v >= ds2 - 2 and stop_v <= ds2 + 2 Then
		useS2_lowstop = true;
	if stop_v <> -1 and stop_v >= shigh - 2 and stop_v <= shigh + 2 Then begin
		useSH_lowstop = true;
	end else if stop_v <> -1 then begin
		useGeneric_lowstop = true;
	end;
	if useGeneric_lowstop = true Then
	begin
		generic_lowstop_v = stop_v;

	end;

	//swinghigh stops

	// only compute the wide stop once per trading session
	if wide_highstop_v = -1 then begin
		wide_highstop_v = SwingHigh(1, high, 5, 50);
		// this stop must have been created by WallStreet traders
		wide_highstop_v_barsago = SwingHighBar(1, high, 5, 50);
		if openBarsAgo = -1 or wide_highstop_v_barsago = -1
			or openBarsAgo < wide_highstop_v_barsago Then
			wide_highstop_v = -1;
	end;

	if wide_highstop_v <> -1 Then
		generic_highstop_v = wide_highstop_v;
	if wide_highstop_v <> -1 and wide_highstop_v >= dp - 2 and wide_highstop_v <= dp + 2 Then
		usePP_wide_lowstop = true;
	if wide_highstop_v <> -1 and wide_highstop_v >= o_high - 1 and wide_highstop_v <= o_high + 1 Then
		useOH_wide_highstop = true;

	stop_v = SwingHigh(1, high, 2, 10);
	if stop_v <> -1 and stop_v >= ds1 - 3 and stop_v <= ds1 + 3 then begin
		useS1_highstop = true;
	end else if stop_v <> -1 and stop_v >= ds2 - 2 and stop_v <= ds2 + 2 then begin
		useS2_highstop = true;
	end else if stop_v <> -1 and stop_v >= dr3 - 2 and stop_v <= dr3 + 2 Then begin
		useR3_highstop = true;
	end else if stop_v <> -1 and stop_v >= dr2 - 2 and stop_v <= dr2 + 2 Then begin
		useR2_highstop = true;
	end else if stop_v <> -1 and stop_v >= dp - 1 and stop_v <= dp + 1 Then begin
		usePP_highstop = true;
	end else if stop_v <> -1 then begin
		useGeneric_highstop = true;
	end;
	if useGeneric_highstop = true Then
	begin
		generic_highstop_v = stop_v;

	end;


	// swinghigh exits
	if Marketposition = 1 then Begin
		if generic_highstop_v[1] > 0 and
			generic_highstop_v < generic_highstop_v[1]
			then
			sell ("llow") this bar on close;
	end;
	if Marketposition = 1 then Begin
		if generic_highstop_v[1] <= 0 and generic_highstop_v >= dr3
			and low crosses under dr3 then
			sell ("dr3_stop") this bar on close;
	end;

	// swinglow exits
	if ema20_x_wlowstop = true or ema20_x_openlow = true and marketposition = -1 then begin
		if generic_lowstop_v[1] > 0 and
			generic_lowstop_v > generic_lowstop_v[1] then
			buy to cover  ("hhigh") this bar on close;
	end;

	{** undesirable but necessary exit **}
	if Marketposition = -1 then Begin
		if low crosses under emaX + 2 then
			buy to cover ("ema50") this bar on close;
	end;
	if Marketposition = -1 then Begin
		if low crosses under dP + 1 then
			buy to cover  ("dp") this bar on close;
	end;
	// note that our version of higher high is based on SwingLow
	// it wont capture RIP UP behavior of the ES!
	if Marketposition = -1 then Begin
		if generic_highstop_v[1] > 0
			and generic_highstop_v > generic_highstop_v[1]
			then
			buy to cover  ("h_highstop") this bar on close;
	end;

	if Marketposition = -1 then Begin
		if low crosses under wide_lowstop_v Then
			buy to cover ("x_wide_stop") next bar on open;
	end;
	if Marketposition = -1 then Begin
		if low crosses under ds2 then Begin
			buy to cover ("ds2_cover") this bar on close;
		end;
	end;
end;
